// Apply Spotless conditionally - 8.x requires Java 17+, use 6.x for older Java
buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17)) {
      classpath "com.diffplug.spotless:spotless-plugin-gradle:8.1.0"
    } else {
      classpath "com.diffplug.spotless:spotless-plugin-gradle:6.13.0"
    }
  }
}

plugins {
  id 'java-library'
  id 'maven-publish'
  id 'org.jreleaser' version '1.18.0' // Stay at 1.18. Version 1.20.0 fails to publish.
}

apply plugin: "com.diffplug.spotless"

group = 'cloud.eppo'
version = '4.0.0-SNAPSHOT'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
  withJavadocJar()
  withSourcesJar()
}

dependencies {
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.21.0'
  implementation 'com.github.zafarkhaja:java-semver:0.10.2'
  implementation "com.squareup.okhttp3:okhttp:4.12.0"
  // For LRU and expiring maps
  implementation 'org.apache.commons:commons-collections4:4.5.0'
  implementation 'org.slf4j:slf4j-api:2.0.17'
  testImplementation 'org.slf4j:slf4j-simple:2.0.17'
  testImplementation platform('org.junit:junit-bom:5.14.3')
  testImplementation("com.squareup.okhttp3:mockwebserver:4.12.0")
  testImplementation 'org.junit.jupiter:junit-jupiter'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  testImplementation 'org.skyscreamer:jsonassert:1.5.3'
  testImplementation 'commons-io:commons-io:2.21.0'
  testImplementation 'com.google.truth:truth:1.4.5'
  testImplementation ('org.mockito:mockito-core:4.11.0') {
    exclude group: 'net.bytebuddy', module: 'byte-buddy' // mockito 4's version doesn't work with Java 21
  }
  testImplementation 'net.bytebuddy:byte-buddy:1.18.4' // Use the latest available version
  testImplementation 'org.mockito:mockito-inline:4.11.0'
}

test {
  useJUnitPlatform()
  testLogging {
    events "started", "passed", "skipped", "failed"
    exceptionFormat "full"
    showExceptions true
    showCauses true
    showStackTraces true
  }
}

spotless {
  ratchetFrom 'origin/main'

  format 'misc', {
    target '*.gradle', '.gitattributes', '.gitignore'

    trimTrailingWhitespace()
    // Use the appropriate method based on Spotless version
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17)) {
      leadingTabsToSpaces(2)  // Spotless 8.x
    } else {
      indentWithSpaces(2)     // Spotless 6.x
    }
    endWithNewline()
  }
  java {
    // Use google-java-format versions compatible with the Java runtime
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17)) {
      googleJavaFormat('1.17.0')  // For Java 17+ with Spotless 8.x
    } else {
      googleJavaFormat('1.7')     // For Java 8-16 with Spotless 6.x
    }
    formatAnnotations()
  }
}

tasks.register('testJar', Jar) {
  archiveClassifier.set('tests')
  from sourceSets.test.output
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId = 'cloud.eppo'
      artifactId = 'sdk-common-jvm'

      from components.java
      artifact testJar // Include the test-jar in the published artifacts
      versionMapping {
        usage('java-api') {
          fromResolutionOf('runtimeClasspath')
        }
        usage('java-runtime') {
          fromResolutionResult()
        }
      }
      pom {
        name = 'Eppo JVM SDK shared library'
        description = 'Eppo SDK for JVM shared library'
        url = 'https://github.com/Eppo-exp/sdk-common-jvm'
        licenses {
          license {
            name = 'MIT License'
            url = 'http://www.opensource.org/licenses/mit-license.php'
          }
        }
        developers {
          developer {
            name = 'Eppo'
            email = 'https://geteppo.com'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/Eppo-exp/sdk-common-jvm.git'
          developerConnection = 'scm:git:ssh://github.com/Eppo-exp/sdk-common-jvm.git'
          url = 'https://github.com/Eppo-exp/sdk-common-jvm'
        }
      }
    }
  }
  repositories {
    maven {
      url = layout.buildDirectory.dir('staging-deploy')
    }
  }
}

jreleaser {
  signing {
    active = 'ALWAYS'
    armored = true
    verify = false
    mode = 'MEMORY'
  }
  deploy {
    maven {
      mavenCentral {
        'release-deploy' {
          active = 'RELEASE'
          url = 'https://central.sonatype.com/api/v1/publisher'
          stagingRepository('build/staging-deploy')
        }
      }
      nexus2 {
        'snapshot-deploy' {
          active = 'SNAPSHOT'
          snapshotUrl = 'https://central.sonatype.com/repository/maven-snapshots'
          url = 'https://central.sonatype.com/repository/maven-snapshots'
          applyMavenCentralRules = true
          snapshotSupported = true
          closeRepository = true
          releaseRepository = true
          stagingRepository('build/staging-deploy')
        }
      }
    }
  }
}

// Custom task to ensure we can conditionally publish either a release or snapshot artifact
// based on a command line switch. See github workflow files for more details on usage.
tasks.register('checkVersion') {
  doLast {
    if (!project.hasProperty('release') && !project.hasProperty('snapshot')) {
      throw new GradleException("You must specify either -Prelease or -Psnapshot")
    }
    if (project.hasProperty('release') && project.version.endsWith('SNAPSHOT')) {
      throw new GradleException("You cannot specify -Prelease with a SNAPSHOT version")
    }
    if (project.hasProperty('snapshot') && !project.version.endsWith('SNAPSHOT')) {
      throw new GradleException("You cannot specify -Psnapshot with a non-SNAPSHOT version")
    }
    project.ext.shouldPublish = true
  }
}

// Ensure checkVersion runs before publishing
tasks.named('publish').configure {
  dependsOn checkVersion
}

javadoc {
  failOnError = false
  options.addStringOption('Xdoclint:none', '-quiet')
  options.addBooleanOption('failOnError', false)
  if (JavaVersion.current().isJava9Compatible()) {
    options.addBooleanOption('html5', true)
  }
}
