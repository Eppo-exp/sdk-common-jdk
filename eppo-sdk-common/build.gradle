plugins {
  id 'java-library'
  id 'maven-publish'
}
apply plugin: 'com.diffplug.spotless'

group = 'cloud.eppo'
version = '4.0.0-SNAPSHOT'

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
  withSourcesJar()
  withJavadocJar()
}

// Include root (framework) sources in this module so we compile one codebase — no project dependency
sourceSets {
  main {
    java.srcDirs project(':').file('src/main/java'), 'src/main/java'
    resources.srcDirs project(':').file('src/main/resources'), 'src/main/resources'
  }
}

tasks.named('sourcesJar', Jar).configure {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

dependencies {
  // Framework dependencies (from root) — needed to compile the inlined framework sources
  api 'org.jetbrains:annotations:26.0.1'
  implementation 'com.github.zafarkhaja:java-semver:0.10.2'
  implementation 'org.apache.commons:commons-collections4:4.5.0'

  implementation 'com.squareup.okhttp3:okhttp:4.12.0'
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.1'

  implementation 'org.slf4j:slf4j-api:2.0.17'

  // Test dependencies
  testImplementation platform('org.junit:junit-bom:5.14.1')
  testImplementation 'org.junit.jupiter:junit-jupiter'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'
  testImplementation 'com.google.truth:truth:1.4.5'
}

test {
  useJUnitPlatform()
  testLogging {
    events "started", "passed", "skipped", "failed"
    exceptionFormat "full"
    showExceptions true
    showCauses true
    showStackTraces true
  }
}

spotless {
  ratchetFrom 'origin/main'

  format 'misc', {
    target '*.gradle', '.gitattributes', '.gitignore'

    trimTrailingWhitespace()
    indentWithSpaces(2)
    endWithNewline()
  }
  java {
    target 'src/main/java/**/*.java'  // only this module's sources; framework lives under root
    googleJavaFormat('1.7')
    formatAnnotations()
  }
}

javadoc {
  failOnError = false
  options.addStringOption('Xdoclint:none', '-quiet')
  options.addBooleanOption('failOnError', false)
  if (JavaVersion.current().isJava9Compatible()) {
    options.addBooleanOption('html5', true)
  }
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId = 'cloud.eppo'
      artifactId = 'sdk-common-jvm'

      from components.java

      pom {
        name = 'Eppo SDK Common JVM'
        description = 'Complete Eppo SDK for JVM with default HTTP client and configuration parser'
        url = 'https://github.com/Eppo-exp/sdk-common-jvm'
        licenses {
          license {
            name = 'MIT License'
            url = 'http://www.opensource.org/licenses/mit-license.php'
          }
        }
        developers {
          developer {
            name = 'Eppo'
            email = 'https://geteppo.com'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/Eppo-exp/sdk-common-jvm.git'
          developerConnection = 'scm:git:ssh://github.com/Eppo-exp/sdk-common-jvm.git'
          url = 'https://github.com/Eppo-exp/sdk-common-jvm'
        }
      }
    }
  }
  repositories {
    maven {
      url = rootProject.layout.buildDirectory.dir('staging-deploy')
    }
  }
}
