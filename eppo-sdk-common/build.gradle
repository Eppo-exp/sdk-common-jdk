plugins {
  id 'java-library'
  id 'maven-publish'
}

group = 'cloud.eppo'
version = rootProject.version

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
  withJavadocJar()
  withSourcesJar()
}

dependencies {
  api project(':')  // Depends on root sdk-framework module

  implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.1'
  implementation 'com.squareup.okhttp3:okhttp:4.12.0'
  implementation 'org.slf4j:slf4j-api:2.0.17'
  compileOnly 'org.jetbrains:annotations:24.1.0'

  testImplementation platform('org.junit:junit-bom:5.14.1')
  testImplementation 'org.junit.jupiter:junit-jupiter'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'
  testImplementation 'org.skyscreamer:jsonassert:1.5.3'
  testImplementation 'commons-io:commons-io:2.20.0'
  testImplementation 'com.google.truth:truth:1.4.5'
  testImplementation ('org.mockito:mockito-core:4.11.0') {
    exclude group: 'net.bytebuddy', module: 'byte-buddy'
  }
  testImplementation 'net.bytebuddy:byte-buddy:1.18.1'
  testImplementation 'org.mockito:mockito-inline:4.11.0'
  testImplementation 'org.slf4j:slf4j-simple:2.0.17'
}

test {
  useJUnitPlatform()
  testLogging {
    events "started", "passed", "skipped", "failed"
    exceptionFormat "full"
    showExceptions true
    showCauses true
    showStackTraces true
  }
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId = 'cloud.eppo'
      artifactId = 'eppo-sdk-common'

      from components.java
      versionMapping {
        usage('java-api') {
          fromResolutionOf('runtimeClasspath')
        }
        usage('java-runtime') {
          fromResolutionResult()
        }
      }
      pom {
        name = 'Eppo SDK Common - Jackson/OkHttp Implementation'
        description = 'Jackson and OkHttp implementations for Eppo SDK'
        url = 'https://github.com/Eppo-exp/sdk-common-jvm'
        licenses {
          license {
            name = 'MIT License'
            url = 'http://www.opensource.org/licenses/mit-license.php'
          }
        }
        developers {
          developer {
            name = 'Eppo'
            email = 'https://geteppo.com'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/Eppo-exp/sdk-common-jvm.git'
          developerConnection = 'scm:git:ssh://github.com/Eppo-exp/sdk-common-jvm.git'
          url = 'https://github.com/Eppo-exp/sdk-common-jvm'
        }
      }
    }
  }
  repositories {
    maven {
      url = layout.buildDirectory.dir('staging-deploy')
    }
  }
}

javadoc {
  failOnError = false
  options.addStringOption('Xdoclint:none', '-quiet')
  options.addBooleanOption('failOnError', false)
  if (JavaVersion.current().isJava9Compatible()) {
    options.addBooleanOption('html5', true)
  }
}
